buildscript {
	ext {
		grailsVersion = project.grailsVersion
	}
	repositories {
		mavenLocal()
		maven { url 'https://repo.grails.org/grails/core' }
		mavenCentral()
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
	}
}

version = projectVersion
group = 'org.grails.plugins'

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

repositories {
	mavenLocal()
	maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
	compileOnly "javax.servlet:javax.servlet-api:$javaxServletApiVersion"
	compileOnly "org.springframework.boot:spring-boot-starter-logging"
	compileOnly "org.grails:grails-web-boot"

	profile "org.grails.profiles:web-plugin"
	compile "org.grails.plugins:spring-security-core:$springSecurityCoreVersion"

	compile "org.jasig.cas.client:cas-client-core:$casClientCoreVersion", {
		exclude module: 'slf4j-api'
	}
	compile "org.springframework.security:spring-security-cas:$springSecurityVersion", {
		['spring-beans', 'spring-context', 'spring-core', 'spring-security-core',
		 'spring-security-web', 'spring-web'].each { exclude module: it }
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

// apply from: "${rootProject.projectDir}/gradle/artifactoryPublish.gradle"
